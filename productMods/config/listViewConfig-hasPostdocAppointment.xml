<?xml version="1.0" encoding="ISO-8859-1"?>
<!-- $This file is distributed under the terms of the license in /doc/license.txt$ -->

<!-- See guidelines in vitro/doc/list_view_configuration_guidelines.txt -->

<list-view-config>
    <query-select>
        PREFIX afn:   &lt;http://jena.hpl.hp.com/ARQ/function#&gt;
        PREFIX core:  &lt;http://vivoweb.org/ontology/core#&gt;
        PREFIX rdfs:  &lt;http://www.w3.org/2000/01/rdf-schema#&gt;
        PREFIX vitro: &lt;http://vitro.mannlib.cornell.edu/ns/vitro/0.7#&gt;
        PREFIX bprofile: &lt;http://vivo.brown.edu/ontology/profile#&gt;

        SELECT DISTINCT <collated>?subclass</collated>
                        ?pdoc
                        ?pdocText
                        ?org ?orgText
                        ?hosp ?hospText
                        ?spec ?specText
                        ?start ?end
                        ?pdocCity ?pdocState ?pdocCountry
        WHERE {
            ?subject ?property ?pdoc .
            ?pdoc rdfs:label ?pdocText .
            OPTIONAL {
                    <collated>
                        ?pdoc vitro:mostSpecificType ?subclass
                    </collated>
                }
            OPTIONAL { ?pdoc bprofile:hasPostdocAppointmentOrganization ?org .
                        ?org rdfs:label ?orgText }
            OPTIONAL { ?pdoc bprofile:hasPostdocAppointmentHospital ?hosp .
                        ?hosp rdfs:label ?hospText }
            OPTIONAL { ?pdoc bprofile:hasPostdocSpecialtyOrDepartment ?spec .
                        ?spec rdfs:label ?specText }
            OPTIONAL { ?pdoc bprofile:startDate ?start }
            OPTIONAL { ?pdoc bprofile:endDate ?end }
            OPTIONAL { ?pdoc bprofile:city ?pdocCity }
            OPTIONAL { ?pdoc bprofile:state ?pdocState }
            OPTIONAL { ?pdoc bprofile:country ?pdocCountry }
        }
        ORDER BY <collated>?subclass</collated> DESC(?start)
    </query-select>

    <query-construct>
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;
        PREFIX vitro: &lt;http://vitro.mannlib.cornell.edu/ns/vitro/0.7#&gt;
        PREFIX bprofile: &lt;http://vivo.brown.edu/ontology/profile#&gt;

        CONSTRUCT {
            ?subject ?property ?pdoc .
            ?pdoc rdfs:label ?pdocText .
            ?pdoc vitro:mostSpecificType ?subclass .
            ?pdoc bprofile:hasPostdocAppointmentOrganization ?org .
            ?org rdfs:label ?orgText .
            ?pdoc bprofile:hasPostdocAppointmentHospital ?hosp .
            ?hosp rdfs:label ?hospText .
            ?pdoc bprofile:hasPostdocSpecialtyOrDepartment ?spec .
            ?spec rdfs:label ?specText .
            ?pdoc bprofile:startDate ?start .
            ?pdoc bprofile:endDate ?end .
            ?pdoc bprofile:city ?pdocCity .
            ?pdoc bprofile:state ?pdocState .
            ?pdoc bprofile:country ?pdocCountry .
        } WHERE {
            {
                ?subject ?property ?pdoc .
                ?pdoc rdfs:label ?pdocText .
            }
            UNION {
                ?subject ?property ?pdoc .
                ?pdoc vitro:mostSpecificType ?subclass .   
            }
            UNION {
                ?subject ?property ?pdoc .
                ?pdoc bprofile:hasPostdocAppointmentOrganization ?org .
                ?org rdfs:label ?orgText .         
            }
            UNION {
                ?subject ?property ?pdoc .
                ?pdoc bprofile:hasPostdocAppointmentHospital ?hosp .
                ?hosp rdfs:label ?hospText .         
            }
            UNION {
                ?subject ?property ?pdoc .
                ?pdoc bprofile:hasPostdocSpecialtyOrDepartment ?spec .
                ?spec rdfs:label ?specText .         
            }
            UNION {
                ?subject ?property ?pdoc .
                ?pdoc bprofile:startDate ?startDate .
                BIND(year(?startDate) as ?start) .            
            }
            UNION {
                ?subject ?property ?pdoc .
                ?pdoc bprofile:endDate ?endDate .
                BIND((IF
                    (year(?endDate) &lt; year(NOW()), year(?endDate), "Present")
                    )
                    as ?end) .           
            }
            UNION {
                ?subject ?property ?pdoc .
                ?pdoc bprofile:city ?pdocCity .           
            }
            UNION {
                ?subject ?property ?pdoc .
                ?pdoc bprofile:state ?pdocState .           
            }
            UNION {
                ?subject ?property ?pdoc .
                ?pdoc bprofile:country ?pdocCountry .           
            }
        }
    </query-construct>

    <template>propStatement-hasPostdocAppointment.ftl</template>
</list-view-config>